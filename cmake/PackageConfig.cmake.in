@PACKAGE_INIT@

#@PROJECT_NAME@-config.cmake
#
# Valid Find COMPONENTS:
#  * SHARED - Find shared libraries.
#  * STATIC - Find static libraries.
#
# Output variables set:
#  * @PROJECT_NAME@_LIBRARIES  - set to static libs if available, shared libs otherwise
#  * @PROJECT_NAME@_SHARED_LIBRARIES - shared libaries if availible
#  * @PROJECT_NAME@_STATIC_LIBRARIES - static libaries if availible
#  * @PROJECT_NAME@_BUILD_TYPES - build types provided
#
# Imported interface targets provided:
#  * @PROJECT_NAME@::@PROJECT_NAME@_static - static library target
#  * @PROJECT_NAME@::@PROJECT_NAME@_shared - shared library target:

#Include targets file.  This will create IMPORTED target @PROJECT_NAME@
include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-targets.cmake")
set(@PROJECT_NAME@_SHARED_LIBRARIES)
set(@PROJECT_NAME@_STATIC_LIBRARIES)
set(@PROJECT_NAME@_SHARED_FOUND 0)
set(@PROJECT_NAME@_STATIC_FOUND 0)
if(@GSW_BUILD_STATIC_LIBS@)
    set(@PROJECT_NAME@_STATIC_LIBRARIES @PROJECT_NAME@::@PROJECT_NAME@_static)
    set(@PROJECT_NAME@_LIBRARIES @PROJECT_NAME@::@PROJECT_NAME@_static)
    set(@PROJECT_NAME@_STATIC_FOUND 1)
    if(@GSW_BUILD_SHARED_LIBS@)
        set(@PROJECT_NAME@_SHARED_LIBRARIES @PROJECT_NAME@::@PROJECT_NAME@_shared)
        set(@PROJECT_NAME@_SHARED_FOUND 1)
    endif()
elseif(@GSW_BUILD_SHARED_LIBS@)
    #Only shared libraries are build.  Set @PROJECT_NAME@_LIBRARIES to point to shared target.
    set(@PROJECT_NAME@_SHARED_LIBRARIES @PROJECT_NAME@::@PROJECT_NAME@_shared)
    set(@PROJECT_NAME@_LIBRARIES @PROJECT_NAME@::@PROJECT_NAME@_shared)
    set(@PROJECT_NAME@_SHARED_FOUND 1)
else()
    set(@PROJECT_NAME@_FOUND 0)
    set(@PROJECT_NAME@_NOT_FOUND_MESSAGE "Neither GSW_BUILD_SHARED_LIBS nor GSW_BUILD_STATIC_LIBS was set.  Unable to find any libraries.")
endif()

if(@GSW_BUILD_STATIC_LIBS@)
    get_target_property(@PROJECT_NAME@_BUILD_TYPES @PROJECT_NAME@::@PROJECT_NAME@_static IMPORTED_CONFIGURATIONS)
elseif(@GSW_BUILD_SHARED_LIBS@)
    get_target_property(@PROJECT_NAME@_BUILD_TYPES @PROJECT_NAME@::@PROJECT_NAME@_shared IMPORTED_CONFIGURATIONS)
endif()

check_required_components("@PROJECT_NAME@")
